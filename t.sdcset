*BASIC
NEW
AUTO
REM>SDCSetup
REM rcf@mince.net
:
version$="v1.4"
test%=FALSE
:
DIM code% &500
IF NOT(test%) THEN target%=&2000 ELSE target%=code%
:
REM need 2 bytes in zero page
scratch=&70
:
OSASCI=&FFE3
OSNEWL=&FFE7
XOSCLI=&FFF7
:
REM end_os = misc *command
REM end_sdc = *SDCCONFIG
end_os$=CHR$(1)
end_sdc$=CHR$(2)
:
FOR pass%=4 TO 6 STEP 2
P%=target%
O%=code%
[ OPT pass%
JMP start
.cmdline
EQUW 0
:
.start
\ preserve scatch on stack
LDA scratch
PHA
LDA scratch+1
PHA
\
\ store cmd line base at scratch
LDA cmdline
STA scratch
LDA cmdline+1
STA scratch+1
LDY #0
LDA (scratch),Y
CMP #13
BNE setupcheck
:
\ list available setups
LDX #255
.listmsgloop
INX
LDA listmsg,X
JSR OSASCI
CMP #13
BNE listmsgloop
LDX #255
.listsetupsloop
INX
LDA setuptable,X
BMI listnext
BEQ exit
JSR OSASCI
JMP listsetupsloop
.listnext
JSR OSNEWL
\ skip 0 and ptr LSB
INX
INX
JMP listsetupsloop
:
.exit
\ restore scratch area
PLA
STA scratch+1
PLA
STA scratch
RTS
:
\ check setup for match
.setupcheck
\ initialise setup table ptr
LDX #255
LDY #255
.setupcmp
INX
INY
LDA setuptable,X
BMI endsetup
BEQ unksetup
CMP (scratch),Y
BEQ setupcmp
.setupnext
INX
LDA setuptable,X
BPL setupnext
.setupskipptr
\ skip setup 0 + ptr LSB
INX
INX
LDY #255
JMP setupcmp
:
\ unknown setup error
.unksetup
LDY #255
.unksetuploop
INY
LDA unksetupmsg,Y
JSR OSASCI
CMP #13
BNE unksetuploop
JMP exit
:
.endsetup
\ did entered setup also end?
LDA (scratch),Y
CMP #13
BNE setupskipptr
:
\ matched setup - get commands
LDA setuptable+1,X
STA scratch
LDA setuptable+2,X
STA scratch+1
:
.runcopy
LDY #255
.runcopyloop
INY
LDA (scratch),Y
\ 0 = end of commands
BEQ exit
STA oscmdbuff,Y
\ <32 = end of line
CMP #32
BPL runcopyloop
\ terminating character in X
TAX
\ overwrite with CR
LDA #13
STA oscmdbuff,Y
TYA
\ move ptr to next command
SEC \ saves another INY
ADC scratch
STA scratch
BCC runos
INC scratch+1
:
.runos
\ end character also indicates
\ type of command - other *misc
\ or *SDCCONFIG
CPX #ASC(end_sdc$)
BEQ runsdccmd
LDY #oscmdbuff-oscmdstart-1 AND &FF
LDX #oscmdbuff MOD 256
JMP runcmd
.runsdccmd
LDY #255
LDX #oscmdstart MOD 256
.runcmd
LDA #ASC("*")
JSR OSASCI
.runprintloop
INY
LDA oscmdstart,Y
JSR OSASCI
CMP #13
BNE runprintloop
LDY #oscmdstart DIV 256
JSR XOSCLI
JMP runcopy
.runend
JMP exit
:
.setuptable
EQUS "ZERO"
EQUB -1
EQUW setup_zero
EQUS "B"
EQUB -1
EQUW setup_b_swmmxfs
EQUS "+B-SDC"
EQUB -1
EQUW subsetup_b_sdc
EQUS "+B-MM"
EQUB -1
EQUW subsetup_b_swmmfs
EQUS "+B-OMM"
EQUB -1
EQUW subsetup_b_mmfs
EQUS "+B-ADFS"
EQUB -1
EQUW subsetup_b_adfs
EQUS "M320"
EQUB -1
EQUW setup_master320
EQUS "M"
EQUB -1
EQUW setup_master350
EQUS "+M-SDC"
EQUB -1
EQUW subsetup_master350_sdc
EQUS "+M-DISC"
EQUB -1
EQUW subsetup_master350_disc
EQUS "VERSION"
EQUB -1
EQUW setup_version
EQUB 0
:
.unksetupmsg
EQUS "Unknown setup"
EQUB 13
:
.listmsg
EQUS "Available setups:"
EQUB 13
:
\ setup command sets
.setup_zero
\ resets settings we change
\ also enables DFS on MOS 1.20
\ and 2.00 (since no *UNPLUG)
EQUS "FSNR 0"+end_sdc$
EQUS "F2NR 0"+end_sdc$
EQUS "ROM3 0"+end_sdc$ \ = MAMMXFS
EQUS "ROM4 0"+end_sdc$
EQUS "ROM5 0"+end_sdc$
EQUS "ROMC 0"+end_sdc$
EQUS "ROME 0"+end_sdc$
EQUB 0
:
.setup_b_swmmxfs
\ B/B+ with GoSDC+SWMMXFS
EQUS "FSNR 162"+end_sdc$
EQUS "F2NR 2"+end_sdc$
EQUS "FSRM 4"+end_sdc$
EQUS "F2RM 5"+end_sdc$
EQUS "ROM3 1"+end_sdc$ \ = MAMMXFS
EQUS "ROM4 0"+end_sdc$
EQUS "ROM5 2"+end_sdc$
EQUS "ROMC 1"+end_sdc$ \ = BE
EQUS "ROME 1"+end_sdc$ \ = DFS
EQUB 0
:
.subsetup_b_sdc
\ B/B+ with GoSDC
\ (switch from Model B)
EQUS "FSNR 2"+end_sdc$
EQUS "F2NR 0"+end_sdc$
EQUS "ROM4 2"+end_sdc$
EQUS "ROM5 0"+end_sdc$
EQUB 0
:
.subsetup_b_swmmfs
\ B/B+ with SWMMFS
\ (switch from Model B)
EQUS "FSNR 161"+end_sdc$
EQUS "F2NR 0"+end_sdc$
EQUS "ROM4 2"+end_sdc$
EQUS "ROM5 0"+end_sdc$
EQUB 0
:
.subsetup_b_mmfs
\ B/B+ with MMFS
\ (switch from Model B)
EQUS "FSNR 160"+end_sdc$
EQUS "F2NR 0"+end_sdc$
EQUS "ROM4 2"+end_sdc$
EQUS "ROM5 0"+end_sdc$
EQUB 0
:
.subsetup_b_adfs
\ B/B+ with GoSDC DFS+ADFS
\ (switch from Model B)
EQUS "FSNR 2"+end_sdc$
EQUS "F2NR 3"+end_sdc$
EQUS "ROM4 2"+end_sdc$
EQUS "ROM5 0"+end_sdc$
EQUB 0
:
.setup_master320
\ MOS 3.20 with GoSDC
\ (switch from non-Master MOS)
EQUS "FSNR 4"+end_sdc$
EQUS "F2NR 5"+end_sdc$
EQUS "FSRM 4"+end_sdc$
EQUS "F2RM 5"+end_sdc$
EQUS "ROM3 0"+end_sdc$
EQUS "ROM4 2"+end_sdc$
EQUS "ROM5 0"+end_sdc$
EQUS "ROMC 0"+end_sdc$
EQUS "ROME 0"+end_sdc$
EQUB 0
:
.setup_master350
\ MOS 3.50 with GoSDC (from
\ (switch from non-Master MOS)
EQUS "FSNR 6"+end_sdc$
EQUS "F2NR 7"+end_sdc$
EQUS "FSRM 4"+end_sdc$
EQUS "F2RM 5"+end_sdc$
EQUS "ROM3 0"+end_sdc$
EQUS "ROM4 2"+end_sdc$
EQUS "ROM5 0"+end_sdc$
EQUS "ROMC 0"+end_sdc$
EQUS "ROME 0"+end_sdc$
EQUB 0
:
.subsetup_master350_sdc
\ MOS 3.50 with GoSDC DFS+ADFS
\ (switch from Master)
EQUS "FSNR 6"+end_sdc$
EQUS "F2NR 7"+end_sdc$
EQUS "ROM4 2"+end_sdc$
EQUS "UNPLUG 9"+end_os$
EQUS "UNPLUG &D"+end_os$
EQUS "CONFIGURE FILE 5"+end_os$
EQUB 0
:
.subsetup_master350_disc
\ MOS 3.50 with disc DFS+ADFS
\ (switch from Master)
EQUS "FSNR 0"+end_sdc$
EQUS "F2NR 0"+end_sdc$
EQUS "ROM4 0"+end_sdc$
EQUS "INSERT 9"+end_os$
EQUS "INSERT &D"+end_os$
EQUS "CONFIGURE FILE 9"+end_os$
EQUB 0
:
.setup_version
\ version number as a comment
EQUS "| "+version$+end_os$
EQUB 0
.endcode
]
:
REM align command buffer to page
REM simplifies code and we don't
REM mind wasting bytes
oldP%=P%
P%=(P%+&FF) AND &FF00
O%=O%+P%-oldP%
[ OPT pass%
.oscmdstart
EQUS "SDCCONFIG "
.oscmdbuff
]
NEXT
:
REM blank unused bytes between
REM code and buffer for neatness
IF oscmdstart>endcode THEN FOR byte%=endcode TO oscmdstart-1:code%?(byte%-target%)=0:NEXT
:
size%=O%-code%
PRINT "Assembled at &";~code%;"+";~size%
IF NOT(test%) THEN params$=STR$~code%+"+"+STR$~size%+" "+STR$~target%+" "+STR$~target%:PRINT '"*SAVE .... ";params$'"or use OSCLI(""SAVE ... ""+params$)":END
:
REM test routine
DIM setupcmd 256
cmdline?0=setupcmd MOD 256
cmdline?1=setupcmd DIV 256
REPEAT
INPUT "=>*SDCTOOL SETUP ";setup$
IF LEFT$(setup$,1)="*" THEN OSCLI MID$(setup$,2) ELSE $setupcmd=setup$:CALL code%
PRINT
UNTIL FALSE
